%======================================================%
% Statics of linear sequent calculus
%======================================================%

LF seq : lctx _ → tp → type =

% Hypotheses

| hyp :
   upd Δ _ X X A A 𝟙 𝟘 Δ' → exh Δ'
  → seq Δ A

% Multiplicatives

| R① :
    exh Δ
  → seq Δ ①

| L① :
    upd Δ _ X X ① ① 𝟙 𝟘 Δ'
  → seq Δ' A
  → seq Δ A

| R⊗ :
    seq Δ₁ A → seq Δ₂ B
    → merge Δ₁ Δ₂ Δ
    → seq Δ (A ⊗ B)

| L⊗ :
    upd Δ _ X X (A ⊗ B) (A ⊗ B) 𝟙 𝟘 Δ'
  → ({x:obj} {y:obj} seq (cons (cons Δ' x A 𝟙) y B 𝟙) C)
  → seq Δ C

| R-o :
    ({x:obj} seq (cons Δ x A 𝟙) B)
  → seq Δ (A -o B)

| L-o :
    upd Δ _ X X (A -o B) (A -o B) 𝟙 𝟘 Δ'
  → merge Δ₁ Δ₂ Δ'
  → seq Δ₁ A
  → ({x:obj} seq (cons Δ₂ x B 𝟙) C)
  → seq Δ C

% Additives

| R& :
    seq Δ A → seq Δ B
  → seq Δ (A & B)

| L&1 :
    upd Δ _ X X (A & B) (A & B) 𝟙 𝟘 Δ'
  → ({x:obj} seq (cons Δ' x A 𝟙) C)
  → seq Δ C

| L&2 :
    upd Δ _ X X (A & B) (A & B) 𝟙 𝟘 Δ'
  → ({x:obj} seq (cons Δ' x B 𝟙) C) 
  → seq Δ C

| R⊕1 :
    seq Δ A
  → seq Δ (A ⊕ B)

| R⊕2 :
    seq Δ B
  → seq Δ (A ⊕ B)

| L⊕:
    upd Δ _ X X (A ⊕ B) (A ⊕ B) 𝟙 𝟘 Δ'
  → ({x:obj} seq (cons Δ' x A 𝟙) C)
  → ({x:obj} seq (cons Δ' x B 𝟙) C)
  → seq Δ C

% Structural rules

| cut :
    merge Δ₁ Δ₂ Δ
  → seq Δ₁ A
  → ({x:obj} seq (cons Δ₂ x A 𝟙) C)
  → seq Δ C;

%------------------------------------------------------%
% Sequent calculus (cut-free)
%------------------------------------------------------%

LF seq_cf : lctx _ → tp → type =

% Hypotheses

| hyp_cf :
   upd Δ _ X X A A 𝟙 𝟘 Δ' → exh Δ'
  → seq_cf Δ A

% Multiplicatives

| R①_cf :
    exh Δ
  → seq_cf Δ ①

| L①_cf :
    upd Δ _ X X ① ① 𝟙 𝟘 Δ'
  → seq_cf Δ' A
  → seq_cf Δ A

| R⊗_cf :
    seq_cf Δ₁ A → seq_cf Δ₂ B
    → merge Δ₁ Δ₂ Δ
    → seq_cf Δ (A ⊗ B)

| L⊗_cf :
    upd Δ _ X X (A ⊗ B) (A ⊗ B) 𝟙 𝟘 Δ'
  → ({x:obj} {y:obj} seq_cf (cons (cons Δ' x A 𝟙) y B 𝟙) C)
  → seq_cf Δ C

| R-o_cf :
    ({x:obj} seq_cf (cons Δ x A 𝟙) B)
  → seq_cf Δ (A -o B)


| L-o_cf :
    upd Δ _ X X (A -o B) (A -o B) 𝟙 𝟘 Δ'
  → merge Δ₁ Δ₂ Δ'
  → seq_cf Δ₁ A
  → ({x:obj} seq_cf (cons Δ₂ x B 𝟙) C)
  → seq_cf Δ C

% Additives

| R&_cf :
    seq_cf Δ A → seq_cf Δ B
  → seq_cf Δ (A & B)

| L&1_cf :
    upd Δ _ X X (A & B) (A & B) 𝟙 𝟘 Δ'
  → ({x:obj} seq_cf (cons Δ' x A 𝟙) C)
  → seq_cf Δ C

| L&2_cf :
    upd Δ _ X X (A & B) (A & B) 𝟙 𝟘 Δ'
  → ({x:obj} seq_cf (cons Δ' x B 𝟙) C) 
  → seq_cf Δ C

| R⊕1_cf :
    seq_cf Δ A
  → seq_cf Δ (A ⊕ B)

| R⊕2_cf :
    seq_cf Δ B
  → seq_cf Δ (A ⊕ B)

| L⊕_cf :
    upd Δ _ X X (A ⊕ B) (A ⊕ B) 𝟙 𝟘 Δ'
  → ({x:obj} seq_cf (cons Δ' x A 𝟙) C)
  → ({x:obj} seq_cf (cons Δ' x B 𝟙) C)
  → seq_cf Δ C;