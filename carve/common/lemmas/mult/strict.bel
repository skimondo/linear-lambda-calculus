%======================================================%
% Algebraic properties of multiplicities
% (strict systems)
%======================================================%

% Remark: • is not total since 𝟙 • 𝟙 is undefined

% Functionality: If α₁ ∙ α₂ = α and α₁ ∙ α₂ = α', then α = α'

rec mult_func : [ ⊢ • α₁ α₂ α] → [ ⊢ • α₁ α₂ α'] → [ ⊢ mult_eq α α'] =
  / total /
  fn m1, m2 ⇒ case m1 of
  | [ ⊢ •/1ω] ⇒ let [ ⊢ •/1ω] = m2 in [ ⊢ mult/refl]
  | [ ⊢ •/ω1] ⇒ let [ ⊢ •/ω1] = m2 in [ ⊢ mult/refl]
  | [ ⊢ •/ωω] ⇒ let [ ⊢ •/ωω] = m2 in [ ⊢ mult/refl]
  ;

% Identity: 
% (1) If α₁ ∙ α₂ = α and α₁ is an identity element, then α₂ = α
% (2) For any α, obtain an identity element β such that β • α = α

rec mult_id : [ ⊢ • α₁ α₂ α] → [ ⊢ ident α₁] → [ ⊢ mult_eq α₂ α] =
  / total 1 /
  fn m, u ⇒
  let [ ⊢ ident/ω] = u in
  case m of
  | [ ⊢ •/ω1] ⇒ [ ⊢ mult/refl]
  | [ ⊢ •/ωω] ⇒ [ ⊢ mult/refl]
  ;

LF get_u∙ : mult → type =
| get-u∙ : ident β → • β α α → get_u∙ α;

rec mult_get_id : {α:[ ⊢ mult]} [ ⊢ get_u∙ α] =
  / total /
  mlam α ⇒ case [ ⊢ α] of
  | [ ⊢ 𝟙] ⇒ [ ⊢ get-u∙ ident/ω •/ω1]
  | [ ⊢ ω] ⇒ [ ⊢ get-u∙ ident/ω •/ωω]
  ;

% Zero-sum-freedom: If α₁ ∙ α₂ = α and α is an identity element, then α₁ = α

rec mult_zsfree : [ ⊢ • α₁ α₂ α] → [ ⊢ ident α] → [ ⊢ mult_eq α₁ α] =
  / total /
  fn m, u ⇒
  let [ ⊢ ident/ω] = u in
  let [ ⊢ •/ωω] = m in
  [ ⊢ mult/refl]
  ;

% Commutativity: If α₁ ∙ α₂ = α, then α₂ ∙ α₁ = α

rec mult_comm : [ ⊢ • α₁ α₂ α] → [ ⊢ • α₂ α₁ α] =
  / total /
  fn m ⇒ case m of
  | [ ⊢ •/1ω] ⇒ [ ⊢ •/ω1]
  | [ ⊢ •/ω1] ⇒ [ ⊢ •/1ω]
  | [ ⊢ •/ωω] ⇒ m
  ;

% Associativity: If (α₁ • α₂) • α₃ = α, then α₁ • (α₂ • α₃) = α

LF •assoc : • _ _ _ → • _ _ _ → type =
  •/assoc : • α₂ α₃ α₂₃ → • α₁ α₂₃ α → {M1:• α₁ α₂ α₁₂} {M2:• α₁₂ α₃ α} •assoc M1 M2;

rec mult_assoc : {M1:[ ⊢ • α₁ α₂ α₁₂]} {M2:[ ⊢ • α₁₂ α₃ α]} [ ⊢ •assoc M1 M2] =
  / total /
  mlam M1, M2 ⇒
  case [ ⊢ M1] of
  | [ ⊢ •/1ω] ⇒ let [ ⊢ •/1ω] = [ ⊢ M2] in [ ⊢ •/assoc •/ωω •/1ω _ _]
  | [ ⊢ •/ω1] ⇒ let [ ⊢ •/1ω] = [ ⊢ M2] in [ ⊢ •/assoc •/1ω •/ω1 _ _]
  | [ ⊢ •/ωω] ⇒
    case [ ⊢ M2] of
    | [ ⊢ •/ω1] ⇒ [ ⊢ •/assoc •/ω1 •/ω1 _ _]
    | [ ⊢ •/ωω] ⇒ [ ⊢ •/assoc •/ωω •/ωω _ _]
  ;

% Cancellativity: If α₁ ∙ α₂ = α and α₁ ∙ α₂' = α, then α₂ = α₂'

rec mult_canc : [ ⊢ • α₁ α₂ α] → [ ⊢ • α₁ α₂' α] → [ ⊢ mult_eq α₂ α₂'] =
  / total /
  fn m1, m2 ⇒ case m1 of
  | [ ⊢ •/1ω] ⇒ let [ ⊢ •/1ω] = m2 in [ ⊢ mult/refl]
  | [ ⊢ •/ω1] ⇒ let [ ⊢ •/ω1] = m2 in [ ⊢ mult/refl]
  | [ ⊢ •/ωω] ⇒ let [ ⊢ •/ωω] = m2 in [ ⊢ mult/refl]
  ;

%------------------------------------------------------%
% Properties of unrestricted elements
%------------------------------------------------------%

% Technical lemmas about unrestricted elements (used to prove context lemmas)

rec mult_unr_id : [ ⊢ unr α] → [ ⊢ • α α α] =
  / total /
  fn u ⇒ let [ ⊢ unr/ω] = u in [ ⊢ •/ωω]
  ;

rec mult_ident_unr : [ ⊢ ident α] → [ ⊢ unr α] =
  / total /
  fn u ⇒ let [ ⊢ ident/ω] = u in [ ⊢ unr/ω]
  ;

rec mult_unr_ident : [ ⊢ unr α] → [ ⊢ ident α] =
  / total /
  fn u ⇒ let [ ⊢ unr/ω] = u in [ ⊢ ident/ω]
  ;
